#!/usr/bin/perl

use warnings;
use strict;

use Date::Calc qw(Today Delta_Days);
use DateTime::Format::DateParse;
use Getopt::Long;
use IPC::Run3;

my $help;
my $version;
my $VERBOSE = $ENV{TERM};

my $DAYS_CRIT = 7;
my $DAYS_WARN = 31;

my $OUT = '';
my $RET = 'OK';
my %PERF;

my %STATUS = (
    OK => 0
    ,WARNING => 1
    ,CRITICAL => 2
    ,UNKNOWN => 3
);


GetOptions(
    help => \$help
    ,version => \$version
    ,verbose => \$VERBOSE
) or exit;

my $usage = "$0 [--help] [--version] /path/to/file.crt";

sub parse {
    my ($field, $date) = @_;

    my $days = Date::Calc::Delta_Days(
        split(/-/,$date->ymd) , Today());

    warn $field." ".$date->ymd." : ".$days
    if $VERBOSE;

    $PERF{$field} = abs($days);

    if ($field =~ /notBefore/) {
        if ($days<0) {
            $RET = 'CRITICAL';
            $OUT .= "cert is yet not active. It will be at ".$date->ymd;
        }
    } elsif($field =~ /notAfter/) {
        if ( $days >=1 ) {
            $RET = 'CRITICAL';
            $OUT .= "cert is out of date since ".$date->ymd;
        } else {
            $days = abs($days);
            if ( $days <= $DAYS_CRIT ) {
                $RET = "CRITICAL" 
            } elsif ($days < $DAYS_WARN) {
                $RET = 'WARNING' if $RET eq "OK";
            }
            $OUT .= "cert will be inactive in $days days at "
                .$date->ymd;
        }
    } else {
        die "Error: unknown field $field";
    }
}

my @CRT = @ARGV;

die "Error: crt file required\n$usage\n" if !@CRT;

for my $crt (@CRT) {
    my @cmd = ("/usr/bin/openssl",
            'x509','-in', $crt,'-dates','-noout'
            );

    my ($out, $err, $in);
    run3(\@cmd, \$in, \$out ,\$err);
    warn $out if $VERBOSE;
    if ($err) {
        $OUT.=$err;
        $RET = 'UNKNOWN' if $RET ne 'CRITICAL';
    }
    for my $line (split /\n/,$out) {
        my ($field,$value) = $line =~ /^(.*?)=(.*)/;
        my $date =  DateTime::Format::DateParse->parse_datetime( $value );
        parse($field,$date);
    }
}

my $OUT_PERF = '';
for my $field (sort keys %PERF) {
    $OUT_PERF .= " " if $OUT_PERF;
    $OUT_PERF .= "$field=$PERF{$field}";
}
print "$RET: $OUT | $OUT_PERF\n";

exit $STATUS{$RET};
